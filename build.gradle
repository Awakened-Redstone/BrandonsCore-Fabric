buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name 'CB Maven FS'
        url "http://adel.podgeweb.com:1616/maven/"
    }
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.mod_version}"
group= "com.brandon3055.brandonscore" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BrandonsCore"

jar {
    manifest {
       // attributes 'FMLCorePlugin': 'com.brandon3055.brandonscore.asm.LoadingPlugin', 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLAT': 'DraconicEvolution_at.cfg'
    }
}

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    mappings = "${config.mappings}"
    replace '${mod_version}', project.config.mod_version
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.CCLIB_version}:deobf"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
